find_dependency(Vulkan)

if("@SFML_OS_LINUX@" OR "@SFML_OS_FREEBSD@" OR "@SFML_OS_OPENBSD@" OR "@SFML_OS_NETBSD@")
    if("@SFML_USE_DRM@")
        find_dependency(PkgConfig)
        if(PKG_CONFIG_FOUND)
            pkg_check_modules(drm ${_sfml_required_arg} ${_sfml_quiet_arg} IMPORTED_TARGET libdrm)
            sfml_fail_if("NOT;drm_FOUND" "libdrm.pc couldn't be found")
            pkg_check_modules(gbm ${_sfml_required_arg} ${_sfml_quiet_arg} IMPORTED_TARGET gbm)
            sfml_fail_if("NOT;gbm_FOUND" "gbm.pc couldn't be found")
        endif()
    else()
        find_dependency(X11)
    endif()
endif()

if("@SFML_OS_ANDROID@")
    find_dependency(OpenGL EGL)
elseif(NOT "@SFML_OS_IOS@")
    find_dependency(OpenGL)
endif()

if("@SFML_OS_LINUX@")
    find_dependency(PkgConfig)
    if(PKG_CONFIG_FOUND)
        pkg_check_modules(udev ${_sfml_required_arg} ${_sfml_quiet_arg} IMPORTED_TARGET libudev>=143)
        sfml_fail_if("NOT;udev_FOUND" "libudev.pc with stable release (version>=143) couldn't be found")
    endif()
endif()

if(SFML_FOUND)
    include("${CMAKE_CURRENT_LIST_DIR}/SFMLModuleTargetsWindow.cmake")
endif()
